[ MONTHLY.CANNABIS.FEE.012.928

 **************************************************
 *   THIS CODE IS THE PROPERTY OF SYNERGENT AND   *
 *   CANNOT BE MODIFIED OR DISTRIBUTED WITHOUT    *
 *   CONSENT OF SYNERGENT.   COPYRIGHT 2020       *
 **************************************************

 Created On      : 12/22/2020
 Created By      : D Berry
 Salesforce #    : 42691
 Description     : Run monthly and check for open accounts with type 23
                   if found, charge a specified fee amount to a draft share (if available)



 [------------------------]
 [------------------------]
 [--  REVISION HISTORY  --]
 [------------------------]
 [------------------------]
  
  Modified Date :
  Modified By   :
  Salesforce #  : 
  Modifications :

]

TARGET=ACCOUNT

DEFINE
 #INCLUDE "SLIB.COMMON.DEF"
 #INCLUDE "RD.OUTPUT.DEF"
 ACCOUNTTYPE=NUMBER
 DRAFTSHARETYPE=NUMBER
 SAVINGSSHARETYPE=NUMBER
 FEEAMOUNT=MONEY
 FEESHAREID=CHARACTER
 FEESHAREBALANCE=MONEY
 EXCEPTIONTEXT=CHARACTER
 ERRORTEXT=CHARACTER
 EXCEPTIONOUTPUT=NUMBER
 FEEOUTPUT=NUMBER
 DELIMITER="        "
 EXCEPTIONCOUNT=NUMBER
 FEECOUNT=NUMBER
END

SETUP
 CALL WRITEFEEHEADERS

 ACCOUNTTYPE=NUMBERREAD("Please enter the account type", "Default is 23")
 IF(ACCOUNTTYPE=0) THEN
  ACCOUNTTYPE=23
 
 DRAFTSHARETYPE=NUMBERREAD("Please enter the draft share type", "Default is 29")
 IF(DRAFTSHARETYPE=0) THEN
  DRAFTSHARETYPE=29
 
 SAVINGSSHARETYPE=NUMBERREAD("Please enter the savings share type", "Default is 23")
 IF(SAVINGSSHARETYPE=0) THEN
  SAVINGSSHARETYPE=23
  
 FEEAMOUNT=MONEYREAD("Please enter the fee amount", "Default is $100.00") [checking with Devin on default amount]
 IF(FEEAMOUNT=$0.00) THEN
  FEEAMOUNT=$100.00
 
END

SELECT
 ACCOUNT:TYPE=ACCOUNTTYPE
 AND ACCOUNT:CLOSEDATE=SLIBBLANKDATE
END

PRINT TITLE="FEE EXCEPTIONS" REPORTCATEGORY="FMREPORTS"
 CALL GETSHAREINFO
 IF(FEESHAREID<>"") THEN
 DO
  IF(FEESHAREBALANCE>=FEEAMOUNT) THEN
  DO
   CALL WRITEFEE
  END
  ELSE
  DO
   EXCEPTIONTEXT="Insufficient share balance"
   CALL WRITEEXCEPTION
  END
 END
END

TOTAL
 OUTPUTSWITCH(FEEOUTPUT,ERRORTEXT)
 NEWLINE
 PRINT "-----------------------------------------------"
 NEWLINE
 PRINT "TOTAL FEE COUNT:"
 PRINT FEECOUNT
 
 OUTPUTSWITCH(OUTPUTCHANNELDEFAULT,ERRORTEXT)
 NEWLINE
 PRINT "-----------------------------------------------"
 NEWLINE
 PRINT "TOTAL EXCEPTION COUNT:"
 PRINT EXCEPTIONCOUNT
END

[*********************************************************************]

PROCEDURE GETSHAREINFO
 FEESHAREID=""
 FEESHAREBALANCE=$0.00
 FOR EACH SHARE WITH(SHARE:TYPE=DRAFTSHARETYPE AND SHARE:CLOSEDATE=SLIBBLANKDATE AND SHARE:CHARGEOFFDATE=SLIBBLANKDATE)
 DO
  FEESHAREID=SHARE:ID
  FEESHAREBALANCE=SHARE:BALANCE
 END
 IF(FEESHAREID="") THEN
 DO
  FOR EACH SHARE WITH(SHARE:TYPE=SAVINGSSHARETYPE AND SHARE:CLOSEDATE=SLIBBLANKDATE AND SHARE:CHARGEOFFDATE=SLIBBLANKDATE)
  DO
   FEESHAREID=SHARE:ID
   FEESHAREBALANCE=SHARE:BALANCE
  END
 END
 IF(FEESHAREID="") THEN
 DO
  EXCEPTIONTEXT="No valid share found on account"
  CALL WRITEEXCEPTION
 END
END

PROCEDURE WRITEFEE
 FEECOUNT=FEECOUNT+1
 OUTPUTSWITCH(FEEOUTPUT,ERRORTEXT)
 COL=1  ACCOUNT:NUMBER
 COL=12 "S"
 COL=14 FEESHAREID
 COL=24 FEEAMOUNT
 COL=26 "Transaction Fee"
 NEWLINE
 OUTPUTSWITCH(OUTPUTCHANNELDEFAULT,ERRORTEXT)
END

PROCEDURE WRITEEXCEPTION
 OUTPUTSWITCH(OUTPUTCHANNELDEFAULT,ERRORTEXT)
 EXCEPTIONCOUNT=EXCEPTIONCOUNT+1
 IF(EXCEPTIONCOUNT=1) THEN
  CALL WRITEEXCEPTIONHEADERS
  
 PRINT ACCOUNT:NUMBER + DELIMITER
 IF(FEESHAREID<>"") THEN
  PRINT FEESHAREID
 ELSE
  PRINT "NA"
 PRINT DELIMITER
 PRINT EXCEPTIONTEXT
 NEWLINE
END

PROCEDURE WRITEEXCEPTIONHEADERS
 PRINT "ACCOUNTNUMBER"
 PRINT "     "
 PRINT "SHARE ID"
 PRINT "  "
 PRINT "EXCEPTION"
 NEWLINE
 PRINT "-----------------------------------------------"
 NEWLINE
END

PROCEDURE WRITEFEEHEADERS
 OUTPUTOPEN(OUTPUTDEVREPORT,0,"FEE POSTING","FMREPORT",FEEOUTPUT,ERRORTEXT)
 NEWLINE
 PRINT "Account      ID   Amount Description"
 NEWLINE
 PRINT "-----------------------------------------------"
 NEWLINE
END



